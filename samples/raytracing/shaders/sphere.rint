#version 460
#extension GL_EXT_ray_tracing : require

hitAttributeEXT vec3 attribs; 

// For tighter packing, only use float types
struct Sphere {
    float  x, y, z;
    float radius;
};

// 플랫폼 상관없이 최소한 (4096-32)/16 = 254 sphere 데이터를 넣을 수 있음 
// (실제로는 훨씬 많이 가능: (maxShaderGroupStride-shaderGroupHandleSize) / sizeof(Sphere))
layout(shaderRecordEXT) buffer SphereRecord {
    Sphere record[254];   
};

void main() 
{
    vec3 center = vec3(
        record[gl_PrimitiveID].x, 
        record[gl_PrimitiveID].y, 
        record[gl_PrimitiveID].z);
    float radius = record[gl_PrimitiveID].radius;

    vec3 O = gl_WorldRayOriginEXT;
    vec3 D = gl_WorldRayDirectionEXT;

    vec3 oc = O - center;
    float a = dot(D, D); 
    float b = dot(oc, D);
    float c = dot(oc, oc) - radius * radius;

    float disc = b*b - a*c;
    if (disc < 0.0) 
        return;

    float sqrtDisc = sqrt(disc);
    float t0 = (-b - sqrtDisc) / a;
    float t1 = (-b + sqrtDisc) / a;

    float tMin = gl_RayTminEXT;
    float tMax = gl_RayTmaxEXT;

    float tHit = (t0 > tMin && t0 < tMax) ? t0 :
                 (t1 > tMin && t1 < tMax) ? t1 : -1.0;

    if (tHit > 0.0) 
    {
        vec3 hitPos = O + tHit * D;
        attribs = normalize(hitPos - center);   

        if (reportIntersectionEXT(tHit, 0)) 
        {
        }
    }
}